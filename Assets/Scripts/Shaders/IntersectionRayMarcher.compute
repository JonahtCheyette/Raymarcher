#pragma kernel CSMain
#include "Basics.compute"

float3 light;

RWTexture2D<float4> result;
StructuredBuffer<shape> shapes;
StructuredBuffer<int> intersections;

float distToScene(float3 p) {
    float distance = 3.402823466e+38F;
    uint numShapes, stride;

    shapes.GetDimensions(numShapes, stride);
    for (uint i = 0; i < numShapes; i++) {
        float shapeDistance = sdShape(p, shapes[i]);

        if (i != 0) {
            if (intersections[i - 1] == 0) {
                //union
                distance = min(distance, shapeDistance);
            } else if (intersections[i - 1] == 1) {
                //subtraction
                distance = max(distance, -shapeDistance);
            } else {
                //intersection
                distance = max(distance, shapeDistance);
            }
        } else {
            distance = min(distance, shapeDistance);
        }
    }

    return distance;
}

float3 albedoAtPoint(float3 p) {
    uint numShapes, stride;
    float3 color = 0;

    shapes.GetDimensions(numShapes, stride);
    for (uint i = 0; i < numShapes; i++) {
        float shapeDistance = sdShape(p, shapes[i]);
        if (shapeDistance < surfDistance) {
            if (i != numShapes - 1 && intersections[i] == 2) {
                //intersection
                if (abs(sdShape(p, shapes[i + 1])) - abs(shapeDistance) < 0.00390625) {
                    color = shapes[i + 1].albedo;
                } else {
                    color = shapes[i].albedo;
                    i++;
                }
            } else {
                color = shapes[i].albedo;
            }
        }
    }

    return color;
}

float rayMarch(float3 startPos, float3 dir) {
    float d = 0;

    for (int i = 0; i < maxMarchingSteps; i++) {
        float3 p = startPos + d * dir;
        float dist = distToScene(p);
        d += dist;
        if (d > maxMarchingDistance || dist < surfDistance) {
            break;
        }
    }

    return d;
}

float3 getNormal(float3 p) {
    float2 e = float2(0.01, 0);
    float d = distToScene(p);
    float3 n = float3(d - distToScene(p - e.xyy), d - distToScene(p - e.yxy), d - distToScene(p - e.yyx));
    return normalize(n);
}

float getLighting(float3 p) {
    float3 l;
    l = normalize(light - p);

    float3 n = getNormal(p);
    float dif = dot(n, l);
    float d = rayMarch(p + surfDistance * 2 * n, l);

    if (d < length(light - p)) {
        dif *= 0.1;
    }

    return dif;
}

[numthreads(8, 4, 1)]
void CSMain(uint3 id : SV_DispatchThreadID) {
    // Get the dimensions of the RenderTexture
    uint width, height;
    result.GetDimensions(width, height);

    float3 cameraPos = mul(cameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    float3 rayDir = CreateCameraRay(float2((id.xy) / float2(width, height) * 2.0f - 1.0f));

    float dist = rayMarch(cameraPos, rayDir);
    float3 endPoint = cameraPos + dist * rayDir;

    result[id.xy] = float4(albedoAtPoint(endPoint) * getLighting(endPoint), 1);
}
